/*
 * generated by Xtext
 */
package org.xtext.nordakademie.evaluation.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess
import org.xtext.nordakademie.evaluation.evaluation.Survey
import org.xtext.nordakademie.evaluation.evaluation.Selection
import org.xtext.nordakademie.evaluation.evaluation.Freetext
import org.xtext.nordakademie.evaluation.evaluation.Page
import org.xtext.nordakademie.evaluation.evaluation.Chart
import org.xtext.nordakademie.evaluation.evaluation.Rating
import org.xtext.nordakademie.evaluation.evaluation.Question

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class EvaluationGenerator implements IGenerator {

	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		// first element of ecore-tree (survey)
		val survey = resource.contents.head as Survey
		// multiple pages
		for (page : survey.pages) {
			fsa.generateFile(page.name + '.html', toHtml(survey, page))
		}
	}

	def toHtml(Survey survey, Page page) '''
		
		<html>
			<head>
				<title>«survey.title»</title>
				<link href="../css/evaluation.css" rel="stylesheet" media="screen">
			</head>
			<body>
				<div id="wrapper">
					<div id="header">
						<h1>«survey.title»</h1>
						<p>«survey.greeting»</p>
					</div id=header>
					<div id="main">
						«««next page if existing
							«IF page.forwarding»
								<form action="«page.followingPage.name + '.html'»" method="post">
									«««one question per page
									«select(page.question)»		
	   								<button type="submit" value="submit">next</button>
									<button type="reset" value="reset">reset</button>
								</form>															 
							«ELSE»
								<form action="evaluation.html" method="post">
									«select(page.question)»		
									<button type="submit" value="submit">finish</button>
									<button type="reset" value="reset">reset</button>
								</form>								
							«ENDIF»
					</div id"main">
					<div id="footer">
						<i>Hausarbeit Softwareengineering von Philipp Schaefer & Martin Kuhla</i>
					</div id="footer">
				</div id="wrapper">
			</body>
		</html>	
	'''

	//	Dispatch methods make a set of overloaded methods polymorphic
	def dispatch select(Freetext question) '''
		<p>
			<b>
			<label for="«question.name»">«question.questionText»
			«helptext(question)»
			<p></p>
			<input type="text" id="«question.name»" name="«question.name»" required>
			<label>
			</b>
		</p>
	'''

	def dispatch select(Selection question) '''
		<p><b>
			«question.questionText»<br>
			«helptext(question)»
			<p></p>
			<fieldset>			
			«FOR bullet : question.bullets»
				«««	radioButton: one choice; checkBox: multiple choice
				«IF question.oneChoice»
					<input type="radio" id="«bullet.name»" name="«question.name»" value="«bullet.bulletText»" required/><label for="«bullet.name»">«bullet.bulletText»</label><br>
				«ELSE»
					<label for="«bullet.name»"> <input type="checkbox" id="«bullet.name»" name="«question.name»" value="«bullet.bulletText»"> «bullet.bulletText» </label><br> 		
				«ENDIF»	
				«IF bullet.freetext»
					&nbsp;<input type="text" name="«question.name»" id="«question.name»">
				«ENDIF»					
				<br>
			«ENDFOR»
			</fieldset> </b>	
		</p>		
	'''

	def dispatch select(Chart question) '''
		<p><b>
			«question.questionText»<br>
			«helptext(question)»
			<p></<p>			
			<style> table, td, th { border: 1px solid black; } </style>
			<table> 
			«««first row with graduation statements
			<tr> 
				<th>&nbsp;</th>
				«FOR graduation : question.graduations»
					<th>«graduation.graduationText»</th>
				«ENDFOR»
			</tr> 
			«««rows with bullets (choices) and radio buttons
			«FOR bullet : question.bullets»
			<tr>
				<th>«bullet.bulletText»</th>
			 	«FOR graduation : question.graduations»
			 		<th><input type="radio" id="«bullet.name»" name="«bullet.bulletText»" value=«graduation.graduationText» required></th> 	
			 	«ENDFOR»
			 </tr>
			«ENDFOR»
			</table> 			
		</b></p>	
	'''

	def dispatch select(Rating question) '''
		<p><b>
			<label>«question.questionText»</label><br>
			«helptext(question)»
			<p></<p>			
			«««loop for quantity of possible ratings
			«FOR ratingValue: 1..question.ratingQuantity»
				«ratingValue»<input type="radio" id="«question.name»" name="«question.name»"  value=«ratingValue»" required/>&nbsp;&nbsp;
			«ENDFOR»
		</b></p>
	'''	
	
	def helptext(Question question) '''
		«««	optional helptext
		«IF !question.helpText.nullOrEmpty»
			«question.helpText»<br>
		«ENDIF»
	'''	
}
